buildscript {
    ext {
        springBootVersion = '2.5.4'
        releaseTrainVersion = '2020.0.4'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${releaseTrainVersion}"
        }
        dependencies {
            dependency 'org.projectlombok:lombok:1.16.16'
            dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.9'
        }
    }
}

ext {
    springProjects = [project(':carwash')]
}

subprojects {
    apply plugin: 'java'

    group 'online.carwashservice'
    version '1.0-SNAPSHOT'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
    }
}

configure(springProjects) {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-webflux'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }
}